# 5.2 线程切换

----

我们通过 `switchThread()` 这个函数来完成线程的切换。

```c
// kernel/thread.c

void
switchThread(Thread *self, Thread *target)
{
    switchContext(&self->contextAddr, &target->contextAddr);
}
```

切换线程主要就是切换上下文，并且跳转到目标线程上次结束的位置。

```c
// kernel/thread.c

__attribute__((naked, noinline)) void
switchContext(usize *self, usize *target)
{
    asm volatile(".include \"kernel/switch.asm\"");
}
```

这里使用了两个属性，`naked` 表示不要在这个函数执行前后加入任何的开场白（prologue）和结语（epilogue），通常的编译器会根据函数调用约定，在函数开头自动加入保存寄存器、设置栈寄存器等内容，这部分我们自行来设置。

`noinline` 指示编译器不要将该函数内联，有些编译器会将函数优化为内联的，从而避免了跳转和返回。但是我们切换线程需要借助函数的调用-返回机制，因此需要声明此属性。

这个函数传入了两个参数，分别是指向当前线程上下文保存地址的指针，和目标线程上下文地址的指针，由于目标线程被切换前是最后被保存在栈上的，所以该地址同时还是栈顶地址。

根据[函数调用约定](https://riscv.org/wp-content/uploads/2015/01/riscv-calling.pdf)，传入的两个参数分别被保存在 a0 和 a1 寄存器，即它们分别保存了“当前线程栈顶地址”所在的地址，以及“目标线程栈顶地址”所在的地址。

> [!TIP|label:Note|]
> **RISC-V 函数调用约定中的通用寄存器职能**
>
> | 寄存器  | ABI 名字 | 描述                                                         | Saver  |
> | :---: | :---: | :---: | :---: |
> | x0      | zero     | 硬件连线0                                                    | -      |
> | x1      | ra       | 返回地址                                                     | Caller |
> | x2      | sp       | 栈指针                                                       | Callee |
> | x3      | gp       | 全局指针                                                     | -      |
> | x4      | tp       | 线程指针                                                     | -      |
> | x5-x7   | t0-t2    | 临时寄存器                                                   | Caller |
> | x8      | s0/fp    | 保存的寄存器/帧指针                                          | Callee |
> | x9      | s1       | 保存寄存器<br />保存原进程中的关键数据，<br />避免在函数调用过程中被破坏 | Callee |
> | x10-x11 | a0-a1    | 函数参数/返回值                                              | Caller |
> | x12-x17 | a2-a7    | 函数参数                                                     | Caller |
> | x18-x27 | s2-s11   | 保存寄存器                                                   | Callee |
> | x28-x31 | t3-t6    | 临时寄存器                                                   | Caller |

所以我们不需要保存所有的寄存器，编译器已经帮我们自动保存了 Caller-saved 寄存器了，我们只需要手动保存 Callee-saved 即可。

以下是该函数的实现。

```c
.equ XLENB, 8

    addi  sp, sp, (-XLENB*14)
    sd sp, 0(a0)
    sd ra, 0*XLENB(sp)
    sd s0, 2*XLENB(sp)
    sd s1, 3*XLENB(sp)
    sd s2, 4*XLENB(sp)
    sd s3, 5*XLENB(sp)
    sd s4, 6*XLENB(sp)
    sd s5, 7*XLENB(sp)
    sd s6, 8*XLENB(sp)
    sd s7, 9*XLENB(sp)
    sd s8, 10*XLENB(sp)
    sd s9, 11*XLENB(sp)
    sd s10, 12*XLENB(sp)
    sd s11, 13*XLENB(sp)
    csrr  s11, satp
    sd s11, 1*XLENB(sp)

    ld sp, 0(a1)
    ld s11, 1*XLENB(sp)
    csrw satp, s11
    sfence.vma
    ld ra, 0*XLENB(sp)
    ld s0, 2*XLENB(sp)
    ld s1, 3*XLENB(sp)
    ld s2, 4*XLENB(sp)
    ld s3, 5*XLENB(sp)
    ld s4, 6*XLENB(sp)
    ld s5, 7*XLENB(sp)
    ld s6, 8*XLENB(sp)
    ld s7, 9*XLENB(sp)
    ld s8, 10*XLENB(sp)
    ld s9, 11*XLENB(sp)
    ld s10, 12*XLENB(sp)
    ld s11, 13*XLENB(sp)
    addi sp, sp, (XLENB*14)

    sd zero, 0(a1)
    ret
```

这部分代码和中断上下文保存与恢复很像，主要需要做的就两件事：

1. 将 Callee-saved 寄存器保存在当前栈上，并更新传入的“当前栈顶地址”
2. 从要恢复的线程栈中恢复目标线程的 Callee-saved 寄存器