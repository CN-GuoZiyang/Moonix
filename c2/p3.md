# 2.3 程序运行上下文

----

# 定义上下文

对于被中断打断的程序来说，中断处理程序应当是透明的。流程从中断处理程序恢复到原先的程序时，应当继续执行被打断的流程，并且所有的中间变量都不应当被修改，否则可能导致原先的程序发生错误。

因此，在处理中断前，我们需要保存所有可能被修改的寄存器，并在中断处理完成后恢复。这些包括：所有的通用寄存器，scause、sepc 和 stval 这三个会被硬件自动写入的寄存器。由于中断过程可能伴随特权级的变化，我们还需要多保存一个 sstatus。

我们定义一个结构体 Context 来保存各种寄存器的信息，它代表的原来程序运行的上下文。

```c
// 中断上下文，scause 和 stval 作为参数传递，无需保存
typedef struct
{
    // 32个通用寄存器
    usize x[32];
    // S-Mode 状态寄存器
    usize sstatus;
    // 中断返回地址
    usize sepc;
} Context;
```

scause 和 stval 将在中断处理过程中作为参数传递，由于处理过程对原程序是透明的，所以无需保存这两个寄存器。

# 上下文的保存与恢复

我们将上下文临时保存在内核栈上，之后再跳转到处理函数进行处理，处理完成后从栈上恢复上下文，最后再跳转回原程序被打断的地方。

由于涉及了大量寄存器读写，我们需要用到汇编。

以下内容在 interrupt.asm 中。

```
.altmacro
# 寄存器宽度 8 字节
.set    REG_SIZE, 8
# Context 大小为 34 字节
.set    CONTEXT_SIZE, 34

# 宏：保存寄存器到栈上
.macro SAVE reg, offset
    sd  \reg, \offset*8(sp)
.endm

.macro SAVE_N n
    SAVE  x\n, \n
.endm

# 宏：从栈中恢复寄存器
.macro LOAD reg, offset
    ld  \reg, \offset*8(sp)
.endm

.macro LOAD_N n
    LOAD  x\n, \n
.endm

    .section .text
    .globl __interrupt
    # 中断处理函数需要 4 字节对齐
    .balign 4
# 全局中断处理，保存 Context 并跳转到 handleInterrupt() 处
__interrupt:
    # 移动栈指针，留出 Context 的空间
    addi    sp, sp, -34*REG_SIZE
    
    # 保存通用寄存器，其中 x0 固定为 0
    SAVE    x1, 1
    # 将原来的 sp 写入 2 位置
    addi    x1, sp, 34*REG_SIZE
    SAVE    x1, 2
    # 循环保存 x3 至 x31
    .set    n, 3
    .rept   29
        SAVE_N  %n
        .set    n, n + 1
    .endr

    # 保存 CSR
    csrr    s1, sstatus
    csrr    s2, sepc
    SAVE    s1, 32
    SAVE    s2, 33

    # 调用 handleInterrupt()
    # 将 Context 的地址(栈顶)和 scause、stval 作为参数传入
    mv      a0, sp
    csrr    a1, scause
    csrr    a2, stval
    jal     handleInterrupt

    .globl __restore
# 从 handleInterrupt 返回
# 从 Context 中恢复所有寄存器，并跳转至 Context 中 sepc 的位置
__restore:
    # 恢复 CSR
    LOAD    s1, 32
    LOAD    s2, 33
    csrw    sstatus, s1
    csrw    sepc, s2

    # 恢复通用寄存器
    LOAD    x1, 1
    # 恢复 x3 至 x31
    .set    n, 3
    .rept   29
        LOAD_N  %n
        .set    n, n + 1
    .endr

    # 恢复 sp（这里最后恢复是为了上面可以正常使用 LOAD 宏）
    LOAD    x2, 2
    sret
```

`__interrupt` 函数通过移动 sp 指针来在栈上留出一个 Context 的位置，并保存各种寄存器到栈上。随后将 sp、scause 和 stval 作为参数存入 a0、a1 和 a2，并调用 `handleInterrupt()` 函数。

从 `handleInterrupt()` 返回后，会进入 `__restore` 函数中，这时将栈上的 Context 恢复到寄存器中，并通过 `sret` 指令返回。`sret` 指令会自动返回 sepc 寄存器中存储的地址处。

根据上一节提到的，入口处需要 4 字节对齐。我们可以使用伪指令 `.balign 4` 来对齐 `__interrupt`。

最后不要忘了在 interrupt.c 中引入这段汇编。

```bash
asm(".include \"kernel/interrupt.asm\"");
```

# 从断点返回

我们改变了中断入口程序，在中断初始化中也要做相应的改变，其实就是改变 stvec 寄存器。

```c
void
initInterrupt()
{
    // 设置 stvec 寄存器，设置中断处理函数和处理模式
    extern void __interrupt();
    w_stvec((usize)__interrupt | MODE_DIRECT);
    printf("***** Init Interrupt *****\n");
}
```

我们在 `__interrupt` 函数调用 `handleInterrupt()` 之前传递了三个参数，分别是 sp、scause 和 stval。后两个好理解，sp 是栈顶指针，传递过来做什么呢？

`__interrupt` 函数的开始，就将 sp 向下移动了 34 个字节以存放 Context，所以，sp 寄存器也是一个指向 Context 的指针。

那么现在的 `handleInterrupt()` 函数应该是这样的：

```c
void
handleInterrupt(Context *context, usize scause, usize stval)
{
    switch (scause)
    {
    case 3L:
        breakpoint(context);
        break;
    default:
        printf("Unhandled interrupt!\nscause\t= %p\nsepc\t= %p\nstval\t= %p\n",
		        scause,
		        context->sepc,
		        stval
        );
		panic("");
    }
}
```

3L 是断点中断发生时，CPU 自动设置 scause 的值，这些可以在 RISC-V 官方文档中找到。

至少现在，`handleInterrupt()` 函数不需要四字节对齐了。~~当然对齐也没有什么坏处。~~

我们通过 `breakpoint()` 函数来处理断点。目前的处理过程很简单，我们想跳过断点，继续执行下一条指令。目前 Context 中的 sepc 保存的是 `ebreak` 指令的地址。如果我们不加以修改，在中断返回后还会再去执行一遍 `ebreak`，导致死循环。所以我们要修改 sepc 指向下一条指令。

这里有一个坑点，RISC-V 的指令是不等长的。RV64 的位宽是 4 字节，但是其指令长度并不总是 4 字节。例如 `ebreak` 指令就只有 2 字节长。

```c
void
breakpoint(Context *context)
{
    printf("Breakpoint at %p\n", context->sepc);
    // ebreak 指令长度 2 字节，返回后跳过该条指令
    context->sepc += 2;
}
```

运行一下：

```bash
***** Init Interrupt *****
Breakpoint at 0x0000000080200532
Return here!
```

进入断点中断后，成功地返回到了 `main()` 的下一条语句，输出了 “Return here!”。